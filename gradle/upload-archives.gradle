apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.elpassion.android.commons'
version = commonVersionName()

if (isCI() && isOnMasterBranch()) {

    def propsFile = rootProject.file('setup.properties')
    def setupProperties = new Properties()
    setupProperties.load(new FileInputStream(propsFile))

    ext['signing.secretKeyRingFile'] = rootProject.file("secring.gpg").absolutePath
    ext['signing.keyId'] = setupProperties['signing.keyId']
    ext['signing.password'] = setupProperties['signing.password']
    ext['elSonatypeUserName'] = setupProperties['elSonatypeUserName']
    ext['elSonatypePassword'] = setupProperties['elSonatypePassword']

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    uploadArchives.repositories.mavenDeployer {

        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: elSonatypeUserName, password: elSonatypePassword)
        }
        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: elSonatypeUserName, password: elSonatypePassword)
        }

        pom.project {
            name 'EL Android Commons'
            description 'Libraries used in most EL Passion Android projects.'
            inceptionYear '2016'
            url 'https://github.com/elpassion/android-commons'
            packaging 'aar'

            developers {
                developer {
                    name 'Maciej GÃ³rski'
                    id 'mg6maciej'
                    email 'maciek.gorski@gmail.com'
                    url 'https://github.com/mg6maciej'
                }
                developer {
                    name 'Karol Kowalski'
                    id 'ordon1313'
                    email 'ordon1313@gmail.com'
                    url 'https://github.com/OrdonTeam'
                }
                developer {
                    name 'Kasper Kondzielski'
                    id 'kghost0'
                    email 'kghost0@gmail.com'
                    url 'https://github.com/ghostbuster91'
                }
                developer {
                    name 'Dominik Malantowicz'
                    id 'jozzi05'
                    email 'dominik.malantowicz@gmail.com'
                    url 'https://github.com/jozzi05'
                }
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                url 'https://github.com/elpassion/android-commons'
                connection 'scm:git:git@github.com:elpassion/android-commons.git'
                developerConnection 'scm:git:git@github.com:elpassion/android-commons.git'
            }
        }
    }
}
